pkgname=lib32-libxxf86misc
pkgver=1.0.3
pkgrel=2
pkgdesc="X11 XFree86 misc extension library (32-bit)"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' 'lib32-xf86miscproto')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXxf86misc-${pkgver}.tar.bz2")
sha256sums=('563f4200862efd3334c33a669e0a0aae5bab31f3998db75b87a99a697cc26b5b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd libXxf86misc-${pkgver}

  ./configure --prefix='/usr' --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd libXxf86misc-${pkgver}

  make DESTDIR="${pkgdir}" install

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {,"${pkgdir}"/usr/share/licenses/${pkgname}/}COPYING
}

# vim: ts=2 sw=2 et:
